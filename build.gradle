plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.8
def ver = '0.6.1'

compileJava {
    options.compilerArgs << "-parameters"
}

javadoc {
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.vertx:vertx-core:3.2.1'
    compile 'com.beust:jcommander:1.48'
    compile 'io.github.lukehutch:fast-classpath-scanner:1.9.18'
    compile 'com.alibaba:fastjson:1.2.11'
    compile 'org.webjars.npm:es6-promise:3.1.2'
    compile 'org.webjars.npm:reqwest:2.0.5'
    compile 'org.thymeleaf:thymeleaf:3.0.0.RELEASE'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
    testCompile 'io.vertx:vertx-unit:3.2.1'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.notsyncing.cowherd'
            artifactId 'cowherd-core'
            version ver
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'cowherd-core'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/notsyncing/cowherd.git'

        version {
            name = ver
            vcsTag = ver
        }
    }

    publications = [ 'maven' ]
}